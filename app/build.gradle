apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'org.greenrobot.greendao'

def keystorePropertiesFile = rootProject.file("keystore.properties")

// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()

// Load your keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

def static releaseTime() {
    return new Date().format("yyyy-MM-dd-HH-mm-ss");
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"
    defaultConfig {
        applicationId "com.app.tvproject"
        minSdkVersion 14
        targetSdkVersion 21
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        //极光推送
        ndk {
            //选择要添加的对应cpu类型的.so库。
            abiFilters 'armeabi', 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64', "mips", "mips64"

        }
        manifestPlaceholders = [
                JPUSH_PKGNAME: applicationId,
                JPUSH_APPKEY : "0b6f8eba71baafefb09340ed", //JPush上注册的包名对应的appkey.
                JPUSH_CHANNEL: "developer-default", //暂时填写默认值即可.
        ]
        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }
    }

    //打包秘钥
    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    //修改打包过后的apk名称
    applicationVariants.all { variant ->
        if (variant.buildType.name == "release") {
            variant.outputs.each { output ->
                def outputFile = output.outputFile
                if (outputFile != null && outputFile.name.endsWith(".apk")) {
                    //这里修改apk文件名
                    def fileName = "TvProject-${defaultConfig.versionName}-${releaseTime()}.apk"
//                    def fileName = "Release-Version-${defaultConfig.versionName}-${releaseTime()}.apk"
                    output.outputFile = new File(outputFile.parent, fileName)
                }
            }
        }
    }
    buildTypes {

        debug {
//            buildConfigField "String", "HOST", '"http://119.23.235.164:8081/"'
            buildConfigField "String", "HOST", '"http://192.168.5.147:8082/"'
//            buildConfigField "String", "HOST", '"http://192.168.5.147:8082/"'
//            buildConfigField "String", "HOST", '"http://120.77.172.70:8081/"'
        }
        release {
            buildConfigField "String", "HOST", '"http://119.23.235.164:8081/"'
//            buildConfigField "String", "HOST", '"http://192.168.5.147:8082/"'
//            buildConfigField "String", "HOST", '"http://120.77.172.70:8081/"'
            signingConfig android.signingConfigs.release
            minifyEnabled true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main {
            jniLibs.srcDirs = ['src/main/jniLibs']
            aidl.srcDirs = ['src/main/aidl']
        }
    }
    greendao {
        schemaVersion 2
        daoPackage 'com.zh.greendao'
        targetGenDir 'src/main/java'
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:recyclerview-v7:25.3.0'
    compile 'com.android.support:appcompat-v7:25.3.0'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    testCompile 'junit:junit:4.12'
    compile 'com.bigkoo:convenientbanner:2.0.5'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    //极光推送
    compile 'cn.jiguang.sdk:jpush:3.0.9'
    compile 'cn.jiguang.sdk:jcore:1.1.7'
    //eventbus
    compile 'org.greenrobot:eventbus:3.0.0'
    //fresco
    compile 'com.facebook.fresco:fresco:0.12.0'
    //rxjava rxandroid
    compile 'io.reactivex:rxandroid:1.0.1'
    compile 'io.reactivex:rxjava:1.1.1'
    //    rxpermission
    compile 'com.tbruyelle.rxpermissions:rxpermissions:0.9.3@aar'
    //    rx preferences
    compile 'com.f2prateek.rx.preferences:rx-preferences:1.0.1'
    //retrofit
    compile 'com.squareup.retrofit2:retrofit:2.2.0'
    compile 'com.squareup.retrofit2:converter-gson:2.2.0'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.2.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.4.1'
    //    compile files('libs/Msc.jar')
    //操作数据库
    compile 'org.greenrobot:greendao:3.2.0'
    //    compile 'com.oushangfeng:MarqueeLayout:1.0.4'
    //    compile 'com.shuyu:GSYVideoPlayer:2.1.1'
}
